plugins {
    id 'java'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.0.6"
}

group = 'test-automation'
version = '1.0-SNAPSHOT'

description = "Test automation project for booking.com"

repositories {
    jcenter()
    mavenCentral()
}

ext {
    seleniumVersion = '3.10.0'
    cucumberVersion = '2.3.1'
}

dependencies {
    compile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    compile "com.google.inject:guice:4.2.0"
    compile "io.github.bonigarcia:webdrivermanager:2.1.0"
    compile "io.cucumber:cucumber-java:$cucumberVersion",
            "io.cucumber:cucumber-junit:$cucumberVersion",
            "io.cucumber:cucumber-guice:$cucumberVersion",
            "io.cucumber:cucumber-java8:$cucumberVersion"
}

def cucumberReportPath = 'build/cucumber/report'
def cucumberReportJsonPath = "$cucumberReportPath/cucumber.json"

cucumberReports {
    outputDir = cucumberReportPath
    reports = files(cucumberReportJsonPath)
}

task runTests(type: Test) {
    jvmArgs '-Dcucumber.options= ' +
            '--plugin pretty ' +
            "--plugin json:$cucumberReportJsonPath " +
            "--plugin html:$cucumberReportPath"

    testLogging {
        events "skipped", "failed", "passed", "standard_out"
    }

}